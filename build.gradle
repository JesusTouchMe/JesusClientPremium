buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "jitpack"
            url = "https://jitpack.io/"
        }
        mavenCentral()
        maven { url = "https://repo.spongepowered.org/maven" }
        maven { url = "https://github.com/jagrosh/DiscordIPC" }
    }
    dependencies {
        classpath "com.github.asbyth:ForgeGradle:6f53277"
        classpath 'com.github.LxGaming:MixinGradle:d75e32e'
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'application'
compileJava.options.encoding = 'UTF-8'

version = "1.1.1"
group= "cum.jesus.jesusclient"
archivesBaseName = "jesusclient"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    clientRunArgs += ["--tweakClass gg.essential.loader.stage0.EssentialSetupTweaker", "--mixin mixins.jesusclient.json"]
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url "https://repo.sk1er.club/repository/maven-public" }
    maven { url "https://repo.sk1er.club/repository/maven-releases/" }
}

configurations {
    packageLib
    configurations.implementation.extendsFrom(configurations.packageLib)
}

dependencies {
    annotationProcessor("org.spongepowered:mixin:0.7.11-SNAPSHOT")
    implementation("org.spongepowered:mixin:0.7.11-SNAPSHOT")
    packageLib("gg.essential:loader-launchwrapper:1.1.1")
    implementation("gg.essential:essential-1.8.9-forge:1498")
    packageLib('com.github.jagrosh:DiscordIPC:e29d6d8') {
        exclude module: 'log4j'
    }
}
mixin {
    disableRefMapWarning = true
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.jesusclient.refmap.json"
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest.attributes(
            "FMLCorePluginContainsFMLMod": true,
            "FMLCorePlugin": "cum.jesus.jesusclient.FMLLoadingPlugin",
            "ForceLoadAsMod": true,
            "MixinConfigs": "mixins.jesusclient.json",
            "ModSide": "CLIENT",
            "TweakClass": "gg.essential.loader.stage0.EssentialSetupTweaker",
            "TweakOrder": "0"
    )

    enabled = false
}

shadowJar {
    archiveFileName = jar.archiveFileName
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.packageLib]
    relocate 'com.jagrosh.discordipc', 'cum.jesus.jesusclient.shadow.jagrosh.discordipc'
    mainClassName = ""
}

reobf {
    shadowJar {
        classpath = sourceSets.main.compileClasspath
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceSets {
    main { output.resourcesDir = java.outputDir }
}

tasks.reobfJar.dependsOn(tasks.shadowJar)